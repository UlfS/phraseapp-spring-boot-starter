buildscript {
	ext {
		springBootVersion = '1.5.3.RELEASE'
	}
	repositories {
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id 'eclipse'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
    id 'com.github.ben-manes.versions' version '0.14.0'
    id 'publishing'
}

apply plugin: 'org.springframework.boot'

group = 'com.epages'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
	compile('org.projectlombok:lombok')
	compile('org.springframework.boot:spring-boot-starter')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

bootRepackage.enabled = false

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'phraseapp-spring-boot-starter'
            groupId = project.group

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

bintray {
    user = project.findProperty('bintrayUser')   ?: System.getenv('BINTRAY_USER')
    key  = project.findProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava','sourceJar']
    publish = true
    pkg {
        repo = "maven"
        name = 'phraseapp-spring-boot-starter'
        userOrg = 'epages'
        websiteUrl = 'https://github.com/ePages-de/phraseapp-spring-boot-starter'
        issueTrackerUrl = 'https://github.com/ePages-de/phraseapp-spring-boot-starter/issues'
        vcsUrl = 'https://github.com/ePages-de/phraseapp-spring-boot-starter.git'
        githubRepo = 'ePages-de/phraseapp-spring-boot-starter'
        githubReleaseNotesFile = 'README.md'
        licenses = ['Apache-2.0']
    }
}

publish.dependsOn('bintrayUpload')

scmVersion {
    tag {
        prefix = ''
    }
}

project.version = scmVersion.version